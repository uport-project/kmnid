buildscript {
    ext {
        //general
        kotlin_version = '1.3.50'

        junit_version = "4.12"
        jacoco_version = "0.8.4"

        //mnid module
        kethereum_version = "0.76.1"
        khash_version = "1.0.0-RC2"
        khex_version = "1.0.0-RC3"

        //demo app
        gradle_tools_version = '3.4.0'

        build_tools_version = "28.0.3"
        compile_sdk_version = 28
        target_sdk_version = compile_sdk_version
        min_sdk_version = 19

        support_lib_version = "28.0.0"
        constraint_layout_version = "1.1.3"

        //testing
        test_runner_version = "1.0.2"
        espresso_version = "3.0.2"

        current_release_version = "0.3.3"
    }

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.android.tools.build:gradle:$gradle_tools_version"
    }
}


allprojects {

    apply plugin: 'jacoco'

    jacoco {
        toolVersion = jacoco_version
    }

    repositories {
        jcenter()
        google()
        maven { url 'https://jitpack.io' }
    }

}

subprojects { subproject ->
    afterEvaluate {
        subproject.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}

apply from: "publishing.gradle"

task jacocoReport(type: JacocoReport) {
    subprojects { sub ->
        afterEvaluate {
            //subproject tasks can only be gathered after evaluation
            def testTasks = sub.tasks.withType(Test)
            dependsOn testTasks
            executionData testTasks
        }
        classDirectories.from(file("${sub.buildDir}/classes/java/main"))
        classDirectories.from(file("${sub.buildDir}/classes/kotlin/main"))
    }

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}
